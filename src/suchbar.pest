// search - section

raw_char = _{ ( ASCII_ALPHANUMERIC | "." | "," | "/" | "\\ " ) }
/// green
raw_string = @{ ( ASCII_ALPHANUMERIC | "-" ) ~ raw_char* }
/// red
raw_string_interior = { ( !PEEK ~ ANY )* }
value = ${ raw_string | PUSH("\"" | "'") ~ raw_string_interior ~ POP }
date = ${ ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }
/// brown
from_to = { ( "-" | ".." ) ~ value }
    /// pink
    starts_with = { "^" | "*" }
    /// pink
    ends_with = { "$" | "*" }
term = { (date | value ~ from_to | starts_with? ~ value ~ ends_with?) }
/// orange
field_name = ${ ASCII_ALPHA ~ ( ASCII_ALPHANUMERIC | "_" | "-" )* }
    /// white
    eq = { "=!" | "=>" | "=<" | ">=" | "<=" | ">" | "<" | "!=" | "==" | "=" }
field = { field_name ~ eq ~ term }
primary = _{ field | term | "(" ~ expr ~ ")" | "{" ~ expr ~ "}" | "[" ~ expr ~ "]" }
/// cyan
invert = { "!" | ^"NOT " }
atom = _{ invert? ~ primary }

bin_op = _{ and |  or }
    /// yellow
	and = { "+" | "&&" | ^"AND " }
	/// yellow
	or = { "||" | ^"OR " }

expr = { atom ~ (bin_op? ~ atom)* }

// sort - section
/// blue
down = { "^" }
sort = { ";" ~ ( ","? ~ down? ~ field_name )+ }

query = _{ SOI ~ expr? ~ sort? ~ EOI }

WHITESPACE = _{ " " | "\t" | NEWLINE }
